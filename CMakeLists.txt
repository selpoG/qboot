cmake_minimum_required(VERSION 3.13.0)
project(qboot CXX)

set(CMAKE_CONFIGURATION_TYPES "Debug;Release")

if(NOT CMAKE_BUILD_TYPE)
	set(CMAKE_BUILD_TYPE "Release")
	message(STATUS "CMAKE_BUILD_TYPE not specified: Use Release by default.")
endif()

if (NOT((CMAKE_BUILD_TYPE STREQUAL "Debug") OR (CMAKE_BUILD_TYPE STREQUAL "Release")))
	message(FATAL_ERROR "CMAKE_BUILD_TYPE must be 'Debug' or 'Release' (case-sensitive)")
endif()

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED on)

set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}" ${CMAKE_MODULE_PATH})

if (MSVC)
	set(MPFR_ROOT_DEBUG "${MPFR_ROOT}/Debug")
	set(MPFR_ROOT_RELEASE "${MPFR_ROOT}/Release")
	set(GMP_ROOT_DEBUG "${GMP_ROOT}/Debug")
	set(GMP_ROOT_RELEASE "${GMP_ROOT}/Release")
else()
	set(MPFR_ROOT_DEBUG "${MPFR_ROOT}")
	set(MPFR_ROOT_RELEASE "${MPFR_ROOT}")
	set(GMP_ROOT_DEBUG "${GMP_ROOT}")
	set(GMP_ROOT_RELEASE "${GMP_ROOT}")
endif()

find_package(MPFR REQUIRED)

if(MSVC)
	string(REPLACE "/W3" "/W4" CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
	string(REPLACE "/MD" "/MT" CMAKE_CXX_FLAGS_DEBUG ${CMAKE_CXX_FLAGS_DEBUG})
	string(REPLACE "/MD" "/MT" CMAKE_CXX_FLAGS_RELEASE ${CMAKE_CXX_FLAGS_RELEASE})
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4 /wd4068 /permissive- /MP /Zc:rvalueCast /sdl")
	set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /Gy /Oi")
	if (CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 19.14)
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /experimental:external /external:W0")
		if (IS_DIRECTORY "${MPFR_INCLUDE_DIR_DEBUG}")
			set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /external:I ${MPFR_INCLUDE_DIR_DEBUG}")
			message(STATUS "set external mpfr debug: `${MPFR_INCLUDE_DIR_DEBUG}`")
		endif()
		if (IS_DIRECTORY "${MPFR_INCLUDE_DIR_RELEASE}")
			set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /external:I ${MPFR_INCLUDE_DIR_RELEASE}")
			message(STATUS "set external mpfr release: `${MPFR_INCLUDE_DIR_RELEASE}`")
		endif()
		if (IS_DIRECTORY "${GMP_INCLUDE_DIR_DEBUG}")
			set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /external:I ${GMP_INCLUDE_DIR_DEBUG}")
			message(STATUS "set external gmp debug: `${GMP_INCLUDE_DIR_DEBUG}`")
		endif()
		if (IS_DIRECTORY "${GMP_INCLUDE_DIR_RELEASE}")
			set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /external:I ${GMP_INCLUDE_DIR_RELEASE}")
			message(STATUS "set external gmp release: `${GMP_INCLUDE_DIR_RELEASE}`")
		endif()
	endif()
else()
	if (IS_DIRECTORY "${MPFR_INCLUDE_DIR_DEBUG}")
		set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -isystem ${MPFR_INCLUDE_DIR_DEBUG}")
	endif()
	if (IS_DIRECTORY "${MPFR_INCLUDE_DIR_RELEASE}")
		set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -isystem ${MPFR_INCLUDE_DIR_RELEASE}")
	endif()
	if (IS_DIRECTORY "${GMP_INCLUDE_DIR_DEBUG}")
		set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -isystem ${GMP_INCLUDE_DIR_DEBUG}")
	endif()
	if (IS_DIRECTORY "${GMP_INCLUDE_DIR_RELEASE}")
		set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -isystem ${GMP_INCLUDE_DIR_RELEASE}")
	endif()
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wpedantic -Wno-padded")
	set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O0 -g3")
	if(CMAKE_COMPILER_IS_GNUCXX)
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Weffc++ -Wno-noexcept -Walloca -Wcast-align -Wcast-qual -Wconditionally-supported -Wconversion -Wctor-dtor-privacy -Wdate-time -Wdisabled-optimization -Wdouble-promotion -Wduplicated-cond -Wfloat-conversion -Wfloat-equal -Wlogical-op -Wmissing-declarations -Wmissing-include-dirs -Wmultichar -Wmultiple-inheritance -Wnull-dereference -Wold-style-cast -Woverlength-strings -Woverloaded-virtual -Wpacked -Wredundant-decls -Wrestrict -Wsign-conversion -Wsign-promo -Wstrict-null-sentinel -Wsuggest-override -Wswitch-default -Wswitch-enum -Wuninitialized -Wunused-macros -Wunused-parameter -Wuseless-cast -Wvariadic-macros -Wvector-operation-performance -Wvirtual-inheritance")
	else()
		if (CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 9.0.0)
			set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-ctad-maybe-unsupported")
		endif()
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Weverything -Wno-c++98-compat -Wno-shadow-field-in-constructor -Wno-c++98-compat-pedantic -Wno-global-constructors -Wno-exit-time-destructors -Wno-covered-switch-default")
	endif()
endif()

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_RELEASE ../bin)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_DEBUG ../bin)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_RELEASE ../lib)
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_DEBUG ../lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE ../lib)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG ../lib)

add_subdirectory(algebra)
add_subdirectory(qboot)
add_subdirectory(main)

set_property(DIRECTORY PROPERTY VS_STARTUP_PROJECT main)
